%import .uql_common (ESCAPED_STRING, NUMBER, WS, COMMENT_VALUE, compound_value)
%import .uql_function (function)

expr: and_expr
        | or_expr
?op_value:  OP_NULL
        | OP_BOOL
        | OP_NUMBER
        | OP_FLOAT
        | OP_STRING
        | op_array
        | op_range
        | OP_TIME
?and_expr: op_condition
        | "(" expr ")"
        | and_expr AND_TERMINAL and_expr
?or_expr: op_condition
        | "(" expr ")"
        | or_expr OR_TERMINAL or_expr
?op_condition: op_field_sig OP op_value_sig
        | op_between
        | op_is_null
        | op_not_exists
        | op_exists
        | op_field_sig OP function
        | op_field_eq_field
        | op_empty
        | op_not_empty
        | op_is_not_null
        | op_contains
        | op_startswith
        | op_endswith

op_field_sig: OP_FIELD
        | op_compound_value

op_value_sig: op_value
    | op_compound_value

op_value_or_field: op_value
    | OP_FIELD

op_field_eq_field: op_field_sig OP op_field_sig
op_between: op_field_sig BETWEEN_TERMINAL op_range
op_is_not_null: op_field_sig "IS NOT NULL"i
op_is_null: op_field_sig "IS NULL"i
op_exists: OP_FIELD EXISTS_TERMINAL
op_not_exists: OP_FIELD "NOT"i EXISTS_TERMINAL
op_empty: op_field_sig EMPTY_TERMINAL
op_not_empty: op_field_sig "NOT"i EMPTY_TERMINAL
op_contains: op_field_sig CONTAINS_TERMINAL op_value_sig
op_startswith: op_field_sig STARTSWITH_TERMINAL op_value_sig
op_endswith: op_field_sig ENDSWITH_TERMINAL op_value_sig

OP: /(!=|<=|>=|=>|=<|==|=|>|<)/

// FIELDS FOR CONDITION

op_range: op_value_sig " AND "i op_value_sig
op_array: "[" [op_value ("," op_value)*] "]"
OP_NULL: "NULL"i
OP_BOOL: /(TRUE|FALSE)/i
// OP_FIELD: /(payload|session|event|profile|flow|memory)\@([a-zA-Z0-9\._\-\~\s]+)/
OP_FIELD: /(payload|session|event|profile|flow|memory)\@([a-z0-9][a-z0-9\_\-]*(?:(\.[a-z0-9][a-z0-9\_\-]*|\[\"(?:[^\"\\]|\\.)+\"\]))+|[a-z0-9][a-z0-9\_\-]*)/i
//OP_FIELD: /(payload|session|event|profile|flow|memory)\@([a-zA-Z0-9\_]{1,}))/

OP_STRING: ESCAPED_STRING
OP_VALUE_TYPE: /[a-zA-Z0-9\._]+/
op_compound_value: OP_VALUE_TYPE "(" [op_value_or_field ("," op_value_or_field)*] ")"
OP_NUMBER: /[+-]?([0-9]*[.])?[0-9]+/
OP_FLOAT: NUMBER
OP_TIME: /\d+(m|s|h|d)/

BETWEEN_TERMINAL: /(\r? \n|\s)+BETWEEN\s+/i
AND_TERMINAL: /(\r? \n|\s)+AND(\r? \n|\s)+/i
OR_TERMINAL: /(\r? \n|\s)+OR(\r? \n|\s)+/i
EXISTS_TERMINAL: /EXISTS/i
EMPTY_TERMINAL: /EMPTY/i
CONTAINS_TERMINAL: /CONTAINS/i
STARTSWITH_TERMINAL: /STARTS WITH/i
ENDSWITH_TERMINAL: /ENDS WITH/i

%ignore WS